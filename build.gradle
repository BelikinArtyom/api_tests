plugins {
    id 'java-library'
    id("io.qameta.allure") version "2.12.0"
    id "io.freefair.lombok" version "6.0.0-m2"
}

def restAssuredVersion = "5.3.1",
    allureVersion = "2.21.0"

def apiKey = project.findProperty('api.key') ?: 'reqres-free-v1'
def baseUri = project.findProperty('base.uri') ?: 'https://reqres.in'
def basePath = project.findProperty('base.path') ?: '/api'

allure {
    report {
        version.set("2.19.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks{
            junit5{
                adapterVersion.set("2.29.1")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation (
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:5.9.3",
            'com.github.javafaker:javafaker:1.0.2')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()

    systemProperty 'api.key', apiKey
    systemProperty 'base.uri', baseUri
    systemProperty 'base.path', basePath

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
tasks.register('auto_tests_custom_run', Test) {
    useJUnitPlatform {
        includeTags("AutoRun")
    }
}